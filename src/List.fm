type List <A: Type> {
  nil,
  cons(head: A, tail: List(A)),
}

// Get the tail of a nonempty list
List.tail<A: Type>(xs: List(A)): List(A)
  case xs{
    nil : List.nil<_>
    cons: xs.tail
  }

// Returns the first elements of a list, discards the rest.
List.take<A: Type>(n: Nat, xs: List(A)): List(A)
  case xs {
    nil : List.nil<_>,
    cons: case n {
      zero: List.nil<_>,
      succ: List.cons<_>(xs.head,List.take<_>(n.pred, xs.tail))
    }
  }

// Return elements while a condition is true
List.take_while<A: Type>(f: A -> Bool, xs: List(A)) : List(A)
  case xs{
    nil : List.nil<_>
    cons: case f(xs.head){
      true : List.cons<_>(xs.head,List.take_while<_>(f, xs.tail))
      false: List.nil<_>
    }
  }

// Appends element to the end of the list.
List.append<A: Type>(as: List(A), a: A): List(A)
  case as{
    nil : List.pure<_>(a)
    cons: List.cons<_>(as.head,List.append<_>(as.tail, a))
  }

// A list with only one element.
List.pure<A: Type>(x: A): List(A)
  List.cons<A>(x)(List.nil<A>)

// Removes the first elements of a list.
List.drop<A: Type>(n: Nat, xs: List(A)): List(A)
  case n{
    zero: xs
    succ: case xs{
      nil: List.nil<_>
      cons: List.drop<_>(n.pred,xs.tail)
    }
  }

// Remove elements while a condition is true
List.drop_while<A: Type>(f: A -> Bool, xs: List(A)): List(A)
  case xs{
    nil : List.nil<_>
    cons: case f(xs.head){
      true : List.drop_while<_>(f,xs.tail)
      false: xs
      }
  }

// Removes an element from the list at given index.
List.delete_at<A: Type>(idx: Nat, list: List(A)): List(A)
  case idx{
    zero: List.tail<A>(list)
    succ: case list{
      nil : list
      cons: List.cons<A>(list.head, List.delete_at<A>(idx.pred, list.tail))
    }
  }

// Updates the element at given position
List.update_at<A: Type>(index: Nat, fn: A -> A, list: List(A)): List(A)
  case list{
    nil : List.nil<_>
    cons: case index{
      zero: List.cons<_>(fn(list.head), list.tail)
      succ: List.cons<_>(list.head, List.update_at<A>(index.pred, fn, list.tail))
    }
  }

// Do all elements satisfy a condition?
List.all<A: Type>(cond: A -> Bool, list: List(A)): Bool
  case list{
    nil : Bool.true
    cons: case cond(list.head){
      true : List.all<_>(cond, list.tail)
      false: Bool.false
    }
  }

// Do any element satisfy a condition?
List.any<A: Type>(cond: A -> Bool, list: List(A)): Bool
  case list{
    nil : Bool.false
    cons: case cond(list.head){
      true : Bool.true
      false: List.any<_>(cond, list.tail)
    }
  }
  
// Are all elements true?
List.and(list: List(Bool)): Bool
  List.all<_>((x) x, list)

//Finds the first occurrence that satisfies a condition
List.find<A: Type>(cond: A -> Bool, xs: List(A)): Maybe(A)
  case xs{
  nil : Maybe.none<_>
  cons: case cond(xs.head){
    true : Maybe.some<_>(xs.head)
    false: List.find<_>(cond, xs.tail)
    }
  }

// insert separators between elements, i.e. `[A,B,C] ~> [A,sep,B,sep,C]`
List.intersperse<A: Type>(sep: A, xs: List(A)): List(A)
  case xs{
    nil : List.nil<_>
    cons: case xs.tail{
      nil : List.pure<_>(xs.head)
      cons: List.cons<_>(xs.head,List.cons<_>(sep,List.intersperse<A>(sep,xs.tail)))
    }
  }

// insert separators between elements, i.e. `[A,B,C] ~> [A,sep,B,sep,C]`
List.intercalate<A: Type>(sep: List(A), xs: List(List(A))): List(A)
  List.flatten<A>(List.intersperse<List(A)>(sep,xs))

// Shows a list as String
List.show<A: Type>(f: A -> String, xs: List(A)): String
  String.flatten(["[",String.intercalate(",", List.map<_,_>(f,xs)),"]"])

// Removes all elements that do not satisfy a condition.
List.filter<A: Type>(f: A -> Bool, xs: List(A)): List(A)
  case xs{
    nil : List.nil<_>
    cons: case f(xs.head){
    true : List.cons<_>(xs.head,List.filter<_>(f, xs.tail))
    false: List.filter<_>(f, xs.tail)
    }
  }

// Converts a list to its non-inductive Church encoding.
List.fold<A: Type>(list: List(A)): <P: Type> -> P -> (A -> P -> P) -> P
  <P> (nil, cons)
  case list {
    nil : nil,
    cons: cons(list.head, List.fold<A>(list.tail)<P>(nil, cons))
  }

// Given two lists `xs` and `ys`, applies `f(xs[i],ys[i])` for all `i`.
List.fold_zip<A: Type, B: Type>(as: List(A), bs: List(B))<C: Type>(fn: A -> B -> C, nil: C, cons: C -> C -> C): C
  case as {
    nil : nil,
    cons: case bs {
      nil : nil,
      cons: cons(fn(as.head, bs.head), List.fold_zip<A,B>(as.tail,bs.tail)<C>(fn, nil, cons))
    }
  }

List.foldr<A: Type,B: Type>(b: B, f: A -> B -> B, xs: List(A)): B
  List.fold<A>(xs)<B>(b,f)

//List.fold1<A: Type>(xs: List(A))<ne: List.not_empty<A>(xs)>
  //: <P: Type> -> P -> (A -> P -> P) -> P
  //<P> (nil, cons)
  //case xs:
  //with ne : List.not_empty<_>(xs.self) = ne;
  //| Empty.absurd<_>(ne);
  //| List.fold<_>(xs)<P>(nil,cons);

// Folds over a list, left associative.
List.for<A: Type>(xs: List(A))<B: Type>(b: B, f: A -> B -> B): B
  case xs {
    nil : b,
    cons: List.for<A>(xs.tail)<B>(f(xs.head,b),f)
  }

// Applies a function to all elements of the list.
List.map<A: Type, B: Type>(f: A -> B, as: List(A)): List(B)
  case as {
    nil: List.nil<_>,
    cons: List.cons<_>(f(as.head), List.map<_,_>(f,as.tail)),
  }

// Reverse the order of the elements of the list.
List.reverse<A: Type>(xs: List(A)) : List(A)
  List.reverse.go<_>(xs,List.nil<_>)

List.reverse.go<A: Type>(xs: List(A), res: List(A)): List(A)
  case xs {
    nil: res,
    cons: List.reverse.go<_>(xs.tail,List.cons<_>(xs.head,res))
  }

// Concatenates two lists.
List.concat<A: Type>(as: List(A), bs: List(A)): List(A)
  case as {
    nil: bs,
    cons: List.cons<_>(as.head, List.concat<_>(as.tail,bs))
  }

// Flattens a list of lists into a single list.
List.flatten<A: Type>(xs: List(List(A))): List(A)
  case xs {
    nil: List.nil<_>,
    cons: List.concat<_>(xs.head, List.flatten<_>(xs.tail))
  }

// Verify if a list is empty.
List.is_empty<A: Type>(list: List(A)): Bool
  case list {
    nil: Bool.true,
    cons: Bool.false,
  }

// Computes the length of the list.
List.length<A: Type>(xs: List(A)): Nat
  List.length.go<_>(xs, 0)

// Computes the length of the list.
List.length.go<A: Type>(xs: List(A), n: Nat): Nat
  case xs {
    nil : n,
    cons: List.length.go<A>(xs.tail, Nat.succ(n))
  }

// A range of nats.
List.range.nat.go(nat: Nat, list: List(Nat)): List(Nat)
  case nat {
    zero: list,
    succ: List.range.nat.go(nat.pred, List.cons<_>(nat.pred, list)),
  }

List.range.nat(nat: Nat): List(Nat)
  List.range.nat.go(nat, List.nil<_>)

// Compares the equality of two lists.
List.eql<A: Type>(eql: A -> A -> Bool, a: List(A), b: List(A)): Bool
  case a {
    nil: case b {
      nil: Bool.true,
      cons: Bool.false,
    },
    cons: case b {
      nil: Bool.false,
      cons: Bool.and(eql(a.head, b.head), List.eql<A>(eql, a.tail, b.tail)),
    },
  }

// Removes the first elements of a list.
List.drop<A: Type>(n: Nat, xs: List(A)):  List(A)
  case n {
    zero: xs
    succ: case xs {
      nil : List.nil<_>
      cons: List.drop<_>(n.pred,xs.tail)
    }
  }

// Apply a function f to transform a List(A) into a List(B).
List.mapped<A: Type>(as: List(A))<B: Type>(f: A -> B): List(B)
  case as {
    nil: List.nil<_>,
    cons: List.cons<_>(f(as.head),List.mapped<A>(as.tail)<B>(f))
  }

// Returns the element at given position.
List.at<A: Type>(index: Nat, list: List(A)): Maybe(A)
  case list {
    nil: Maybe.none<_>,
    cons: case index {
      zero: Maybe.some<_>(list.head),
      succ: List.at<_>(index.pred, list.tail),
    }
  }

// Returns the element at given position, backwards.
List.at_last<A: Type>(index: Nat, list: List(A)): Maybe(A)
  List.at<A>(index, List.reverse<_>(list))

List.init<A: Type>(list: List(A)): List(A)
  case list {
    cons: case list.tail {
      cons: List.cons<_>(list.head, List.init<A>(list.tail)),
      nil: List.nil<_>,
    },
    nil: List.nil<_>,
  }
  
List.split_at<A: Type>(n : Nat, xs: List(A)) : Pair(List(A),List(A))
  case xs {
    nil : { List.nil<_>, List.nil<_> },
    cons: case n {
      zero: { List.nil<_>, xs },
      succ: 
        open List.split_at<_>(n.pred, xs.tail) as res
        { List.cons<_>(xs.head, res.fst), res.snd }
    }
  }

List.zip<A: Type, B: Type>(as: List(A), bs: List(B)): List(Pair(A,B))
  case as {
    nil: List.nil<_>,
    cons: case bs {
      nil: List.nil<_>,
      cons: List.cons<_>(Pair.new<_,_>(as.head, bs.head), List.zip<A,B>(as.tail, bs.tail)),
    }
  }

// The functor instance for list
// List.functor: Functor(List)
//   Functor.new<List>(List.map)

// // Proof that List.functor conforms to the functor laws
// List.functor.verified: VerifiedFunctor(List, List.functor)
//   VerifiedFunctor.new<List, List.functor>(List.map.id, List.map.comp)

// May get the head of a list
List.head<A: Type>(xs: List(A)): Maybe(A)
  case xs {
    nil : Maybe.none<_>,
    cons: Maybe.some<_>(xs.head)
  }

// May get the head of a list
List.head_with_default<A: Type>(default: A, xs: List(A)): A
  case xs {
    nil : default,
    cons: xs.head
  }

// Finds the first element that satisfies a condition and its index.
List.ifind<A: Type>(xs: List(A), f: A -> Nat -> Bool): Maybe(Pair(A,Nat))
  List.ifind.go<A>(xs,f,Nat.zero)

List.ifind.go<A: Type>(xs: List(A), f: A -> Nat -> Bool, i: Nat): Maybe(Pair(A,Nat))
  case xs {
    nil : Maybe.none<_>,
    cons: case f(xs.head,i) {
      true : Maybe.some<_>(Pair.new<_,_>(xs.head)(i)),
      false: List.ifind.go<_>(xs.tail,f,Nat.succ(i))
    }
  }

// Folds over a list with an index, right associative
// List.ifoldr.u32<A: Type,B: Type>(b: B, f: U32 -> A -> B -> B, xs: List(A)): B
//   case xs {
//     nil : b,
//     cons: f(0u, xs.head, List.ifoldr.u32<_,_>(b, (i) f(U32.add(1u, i)), xs.tail))
//   }

// Folds over a list, left associative
// List.ifor<A: Type>(xs: List(A))<B: Type>(b: B, f: Nat -> A -> B -> B): B
//   case xs {
//     nil : b,
//     cons: List.ifor<A>(xs.tail)<B>(f(0,xs.head,b), (n) f(Nat.succ(n)))
//   }

// Folds over a list, left associative
// List.ifor.u32<A: Type>(xs: List(A))<B: Type>(b: B, f: U32 -> A -> B -> B): B
//   case xs {
//     nil : b,
//     cons: List.ifor.u32<A>(xs.tail)<B>(f(0u,xs.head,b), (n) f(U32.add(n,1u)))
//   }

// Map over a list with the index
List.imap<A: Type,B: Type>(f: Nat -> A -> B, xs: List(A)): List(B)
  case xs {
    nil : List.nil<_>,
    cons: List.cons<_>(f(0,xs.head),List.imap<_,_>((n) f(Nat.succ(n)),xs.tail))
  }

// Adds the index of each element in a list
List.indices<A: Type>(xs: List(A)): List(Pair(Nat,A))
  List.imap<A,Pair(Nat,A)>((i,x) {i,x}, xs)

// Adds the index of each element in a list
List.indices.u32<A: Type>(xs: List(A)): List(Pair(U32,A))
  List.imap<A,Pair(U32,A)>((i,x) {Nat.to_u32(i),x}, xs)

// Get the all elements of a nonempty list except the last one
// List.init<A: Type>(xs: List(A), not_empty: List.not_empty<A>(xs)) : List(A)
//   case xs:
//   with ne : List.not_empty<A>(xs.self) = not_empty;
//   | Empty.absurd<_>(ne);
//   | case xs.tail:
//     with e : Equal(_, xs.tail.self,xs.tail) = Equal.to<List(A),xs.tail>;
//     | List.nil<_>;
//     | def cne = List.cons_isnt_empty<A>(xs.tail.head,xs.tail.tail)
//       def xne = Equal.rewrite<_,_,_,(x) List.not_empty<A>(x)>(e,cne)
//       List.cons<_>(xs.head,List.init<_>(xs.tail,xne));;

// concrete implementation of insertion sort for nats
List.insert_sort_nat(ns: List(Nat)): List(Nat)
  List.foldr<_,_>(List.nil<_>, List.insert_sort_nat.aux, ns)

List.insert_sort_nat.aux(n: Nat, ns: List(Nat)): List(Nat)
  open List.span<_>(Nat.lte(n), ns) as spanned
  List.concat<_>(spanned.fst, List.cons<_>(n, spanned.snd))


List.span<A: Type>(f: A -> Bool, xs: List(A)): Pair(List(A),List(A))
  case xs {
    nil : {List.nil<_>, List.nil<_>},
    cons: case f(xs.head) {
      true: 
        open List.span<_>(f, xs.tail) as spanned
        { List.cons<_>(xs.head, spanned.fst), spanned.snd }
      false: { List.nil<_>, xs } 
    }
  }

// Add the total of a list of natural numbers
List.sum(xs: List(Nat)) : Nat
  List.sum.go(xs, Nat.zero)

List.sum.go(xs: List(Nat), n: Nat) : Nat
  case xs {
    nil : n,
    cons: List.sum.go(xs.tail,Nat.add(xs.head,n))
  }

// concatenate two list builders
List.Builder.concat<A: Type>(a: List.Builder(A), b: List.Builder(A)): List.Builder(A)
   (x) a(b(x))

// prepend an element
List.Builder.cons<A: Type>(c: A, lb: List.Builder(A)) : List.Builder(A)
  (x) List.cons<_>(c,lb(x))

// A lazy List constructor, useful for efficient concatenation
List.Builder(A: Type): Type
  List(A) -> List(A)

// construct a new builder (it's just the identity function)
List.Builder.new<A: Type> : List.Builder(A)
  (x) x

// append an element
List.Builder.snoc<A: Type>(c: A, lb: List.Builder(A)) : List.Builder(A)
  (x) lb(List.cons<_>(c,x))

// The bind function of the list Monad.
List.bind<A: Type,B: Type>(xs: List(A), f: A -> List(B)): List(B)
  List.flatten<_>(List.map<_,_>(f, xs))

List.chunk<A: Type>(n: Nat, xs: List(A)): Maybe(List(A))
  case n {
    zero: Maybe.some<_>(List.nil<_>),
    succ: case xs {
      nil : Maybe.none<_>,
      cons: Maybe.bind<_,_>(List.chunk<_>(n.pred,xs.tail),
              (x) Maybe.some<_>(List.cons<_>(xs.head,x)))
    }
  }

List.chunks_of<A: Type>(len: Nat, xs: List(A)): List(List(A))
  List.chunks_of.go<_>(len, xs, len, List.nil<_>)

List.chunks_of.go<A: Type>(
  len   : Nat,     // length of each chunk
  list  : List(A), // list to be split
  need  : Nat,     // number of vals to complete chunk
  chunk : List(A)  // current chunk
) : List(List(A))
  case list {
    nil : List.cons<_>(List.reverse<_>(chunk), List.nil<_>),
    cons: case need {
      zero: 
        let head = List.reverse<_>(chunk)
        let tail = List.chunks_of.go<_>(len, list, len, List.nil<_>)
        List.cons<_>(head, tail),
      succ: 
        let chunk = List.cons<_>(list.head, chunk)
        List.chunks_of.go<_>(len, list.tail, need.pred, chunk)
    }
  }

// Proof that List.cons(f(x)) & List.map(f, xs) commute
List.commute_cons_map<A: Type, B: Type>(a :A, ls: List(A), f: A -> B)
  : Equal(List(B),
          List.cons<B>(f(a), List.map<A, B>(f, ls)),
          List.map<A, B>(f, List.cons<A>(a, ls)))
  case ls {
    nil : Equal.refl<_,_>
    cons: Equal.refl<_,_>
  } : Equal(_, List.cons<_>(f(a), List.map<_,_>(f, ls)), List.map<_,_>(f, List.cons<_>(a, ls)))


// The monad instance for list
List.monad: Monad(List)
  Monad.new<List>(List.bind, List.pure)

// A theorem that a list made with List.cons is not the empty list
// List.cons_isnt_empty<A: Type>(
//   x:A,
//   xs: List(A)
// ): List.not_empty<A>(List.cons<A>(x,xs))
//   def y = List.cons<A>(x,xs)
  //case y:
  //with e : Equal(List(A),y,y.self) = Equal.to<List(A),y>;
  //| Empty.absurd<>(List.cons_isnt_nil<A>(x,xs,e));
  //| Unit.new;

// A theorem that List.cons isn't List.nil
// List.cons_isnt_nil<A: Type>(x:A, xs: List(A))
//   : Not(Equal(List(A),List.cons<A>(x,xs), List.nil<A>))
//   def P = ((x) case x { nil: Empty, cons: Unit }) :: List(A) -> Type 
//   (e) Equal.rewrite<List(A), List.cons<A>(x,xs), List.nil<A>,P>(e, Unit.new)

// removes an element from the list at given index
// List.delete_at.u32<A: Type>(idx: U32, list: List(A)): List(A)
//   if U32.eql(idx, 0u) then
//     List.tail<A>(list)
//   else
//     case list {
//       nil : list,
//       cons: List.cons<A>(list.head, List.delete_at.u32<A>(U32.sub(idx,1u), list.tail))
//     }

// Removes an element from the list with a user-supplied equality predicate.
List.delete_by<A: Type>(p: A -> A -> Bool, a: A, as: List(A)): List(A)
  case as {
    nil : List.nil<_>,
    cons: case p(a, as.head) {
      true : List.delete_by<_>(p, a, as.tail),
      false: List.cons<_>(as.head, List.delete_by<_>(p, a, as.tail))
    }
  }

// checks if an element occurs in the list with a user-supplied equality predicate
List.elem<A: Type>(p: A -> A -> Bool, a: A, as: List(A)): Bool 
  case as {
    nil : Bool.false,
    cons: case p(a, as.head) {
      true : Bool.true,
      false: List.elem<A>(p, a, as.tail)
    }
  }

// Finds the last element that satisfies a condition and its index.
List.find_last<A: Type>(xs: List(A), f: A -> Nat -> Bool): Maybe(Pair(A)(Nat))
  List.find_last.go<A>(xs,f,Nat.zero,Maybe.none<_>)

List.find_last.go<A: Type>(
  xs: List(A),
  f: A -> Nat -> Bool,
  n: Nat,
  res: Maybe(Pair(A,Nat))
): Maybe(Pair(A,Nat))
  case xs {
    nil : res,
    cons:
      let res = case f(xs.head,n) {
        true : Maybe.some<_>(Pair.new<_,_>(xs.head,n)),
        false: res
      }
      List.find_last.go<_>(xs.tail,f,Nat.succ(n),res)
  }

// Insert separators between elements, i.e. `[A,B,C] ~> [A,sep,B,sep,C]`.
List.intercalate<A: Type>(sep: List(A), xs: List(List(A))): List(A)
  List.flatten<A>(List.intersperse<List(A)>(sep,xs))

// A list with only one element.
List.pure<A : Type>(x : A) : List(A)
  List.cons<A>(x)(List.nil<A>)

List.range(n: Nat): List(Nat)
  List.range.go(n, List.nil<_>)

List.range.go(n: Nat, xs: List(Nat)): List(Nat)
  case n {
    zero: xs,
    succ: List.range.go(n.pred, List.cons<_>(n, xs))
  }

// insert separators between elements, i.e. `[A,B,C] ~> [A,sep,B,sep,C]`
List.intercalate<A: Type>(sep: List(A), xs: List(List(A))): List(A)
  List.flatten<A>(List.intersperse<List(A)>(sep,xs))

// Insert separators between elements, i.e. `[A,B,C] ~> [A,sep,B,sep,C]`.
List.intersperse<A: Type>(sep: A, xs: List(A)): List(A)
  case xs {
    nil : List.nil<_>,
    cons: case xs.tail {
      nil : List.pure<_>(xs.head),
      cons: List.cons<_>(xs.head,List.cons<_>(sep,List.intersperse<A>(sep,xs.tail)))
    }
  }

// Do any element satisfy a condition?
List.any<A: Type>(cond: A -> Bool, list: List(A)): Bool
  case list {
    nil: Bool.false,
    cons: case cond(list.head) {
      true: Bool.true,
      false: List.any<_>(cond, list.tail),
    }
  }

List.null<A: Type>(xs: List(A)) : Bool
  case xs {
    nil : Bool.true,
    cons: Bool.false
  }
// Is any element true?
List.or(list: List(Bool)): Bool
  List.any<_>((x) x)(list)

List.product(xs: List(Nat)) : Nat
  List.product.go(xs, Nat.zero)

List.product.go(xs: List(Nat), n: Nat) : Nat
  case xs {
    nil : Nat.zero,
    cons: List.product.go(xs.tail,Nat.mul(xs.head,n))
  }

// Turn a builder into a List.
List.run_builder<A: Type>(lb: List.Builder(A)): List(A)
  lb(List.nil<_>)

// List.subsequences<A: Type>(xs: List(A)): List(List(A))
//   List.cons<_>(List.nil<_>,List.subsequences.go<_>(xs))

// List.subsequences.go<A: Type>(xs: List(A)): List(List(A))
//   case xs {
//     nil : List.nil<_>,
//     cons:
//       let f = ((ys,r) List.cons<_>(ys,List.cons<_>(List.cons<_>(xs.head,ys),r)))
//            :: List(A) -> List(List(A)) -> List(List(A))
//       List.cons<_>(
//         List.pure<_>(xs.head),
//         List.foldr<_,_>(List.nil<_>,f,List.subsequences.go<_>(xs.tail))
//       )
//   }

// Convert a list to a builder.
List.to_builder<A: Type>(list: List(A)) : List.Builder(A)
  List.to_builder.go<_>(list, List.Builder.new<_>)

List.to_builder.go<A: Type>(list: List(A), lb: List.Builder(A)) : List.Builder(A)
  case list {
    nil : lb,
    cons: List.to_builder.go<_>(list.tail, List.Builder.snoc<_>(list.head, lb))
  }

List.uncons<A: Type>(xs: List(A)): Maybe(Pair(A,List(A)))
  case xs {
    nil : Maybe.none<_>,
    cons: Maybe.some<_>(Pair.new<_,_>(xs.head,xs.tail))
  }


List.unfoldr<A: Type, B: Type>(f: A -> Maybe(Pair(A,B)), a: A): List(B)
  List.unfoldr.go<A,B>(f, a, List.nil<B>)

List.unfoldr.go<A: Type, B: Type>(f: A -> Maybe(Pair(A,B)), a: A, bs: List(B)): List(B)
  case f(a) as res {
    none: List.nil<B>,
    some: 
      open res.value as pair
      List.unfoldr.go<A,B>(f, pair.fst, List.cons<B>(pair.snd, bs))
  }

// Given two lists `xs` and `ys`, applies `f(xs[i],ys[i])` for all `i`.
List.zip_with<A: Type, B: Type, C: Type>(f: A -> B -> C, as: List(A), bs: List(B))
  : List(C)
  case as {
    nil : List.nil<_>,
    cons: case bs {
      nil : List.nil<_>,
      cons: List.cons<_>(f(as.head,bs.head),List.zip_with<_,_,_>(f,as.tail,bs.tail))
    }
  }